#!/bin/gnuplot

# This GNU Plot script requires the following variables:
#   - application : Name of the application
#   - ysize : Distance between y entries on the Execution Time plot
# 
# Call example:
#   gnuplot -e "application='cholesky'" -e "ysize=100" exec-time_and_speed-up.gpl
#

reset

# Output format
set terminal pdf enhanced font "sans,16" size 10,3

# Set output file
set output "output/" . application . ".pdf"

# Set multi plot
set multiplot layout 1, 2 margins 0.1, 0.98, 0.1, 0.98 spacing 0.2, 0
set tmargin 1 
#set bmargin 0
#set lmargin 1
#set rmargin 1


#
# PLOT 1: EXECUTION TIME
#

#set title "Execution Time"

# Caption position
unset key
#set key outside right center horizontal noreverse noenhanced autotitle nobox

# Set labels
set xlabel "#Workers (#cores)"
set ylabel "Execution Time (s)"

# Style
set style histogram errorbars linewidth 1 gap 1
set style data histograms

set boxwidth 0.9 absolute
set style fill solid 0.5 
set bars front

set yrange [0:*]
set ytics ysize
set grid ytics

# Plot
#1(48)   1       3.68    5.31    4.65    617.66  2874.82 2874.82 2874.82 128.30  597.14  597.14  597.14
plot "data/" . application . ".data" using 9:7:8:xticlabels(1) title "AutoParallel FG" linecolor rgb "#004586", \
        '' using 13:11:12 title "AutoParallel LT" lt 1 lc rgb "#3cb371", \
        '' using 9:xticlabels(1) with lines linecolor rgb "#2b5272" title "", \
        '' using 11:xticlabels(1) with lines linecolor rgb "#29aa63" title ""
 

#
# PLOT 2: SLOW-DOWN
#

#set title "Slow-down"

# Caption position
unset key
#set key inside right top vertical noreverse noenhanced autotitle nobox
set tmargin 1

# Set labels
set xlabel "#Workers (#cores)"
set xtics
set ylabel "Slow-down (u)"

# Style
set style histogram cluster gap 1
set style data histograms

set boxwidth 0.9 absolute
set style fill solid 0.5
set bars front

#set logscale y 2
set yrange [0:192]
set ytics (8, 16, 32, 64, 128, 192, 256, 512, 768, 1024, 2048)
set grid ytics

# Plot
plot "data/" . application . ".data" using 6:xticlabels(1) title "AutoParallel FG" linecolor rgb "#004586", \
        '' using 10 title "AutoParallel LT" lt 1 lc rgb "#3cb371", \
        '' using 6:xticlabels(1) with lines linecolor rgb "#2b5272" title "", \
        '' using 10:xticlabels(1) with lines linecolor rgb "#29aa63" title ""

unset multiplot
